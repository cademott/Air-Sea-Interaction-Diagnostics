#!/bin/csh

#-----------------------------------------------------------------------------------
#
# * Computes (MODEL - OBS) or (EXP - CTRL) files
# * Creates a new directory under MODEL or EXPERIMENT /proc directory
#	named ./proc/MODEL_minus_EXP or ./proc/MODEL_minus_OBS
#
# EXMAPLE:  "SPCCSM" and "ERAI" creates $SPCCSMdir/proc/SPCCSM_minus_ERAI
#
#-----------------------------------------------------------------------------------
#======================== MODEL CASE NAME OR EXPERIMENT NAME =======================
setenv 	modelname 	"SPCCSM"
source 	./AirSea_definitions.sh # handle model-specific logic
setenv 	MODDIR		$FILEDIR"/proc/"
setenv 	MODNAME		$modelname
setenv	MODDATES	$YMDSTRT"-"$YMDLAST

#======================== OBSERVATIONS OR CONTROL CASE NAME ========================
setenv 	modelname 	"ERAI"
source 	./AirSea_definitions.sh # handle model-specific logic
setenv 	OBSDIR		$FILEDIR"/proc/"
setenv 	OBSNAME		$modelname
setenv	OBSDATES	$YMDSTRT"-"$YMDLAST

echo	$MODDATES
echo	$OBSDATES


setenv	DIRO		$MODDIR$MODNAME"_minus_"$OBSNAME"/"
mkdir 	-p $DIRO
echo	"output directory:  "$DIRO
rm 		-f ./FileManipulationScripts/attr.sh
rm		-f ./FileManipulationScripts/ModelGridInfo

#============= Level 1 difference files ==================
foreach pName ("make_L1.3a_mean_u850" "make_L1.4_mean_stdev_map" "make_L1.5_stdev_map" "make_L1.6_U850_WesterlyPct" )

	echo "Processing output generated by "$pName
	foreach modFile ($MODDIR$MODNAME.$pName.$MODDATES.*.nc)

		# generate MODEL grid info (for each file, even though redundant)
		echo $modFile
		cdo --silent griddes $modFile >> ./FileManipulationScripts/ModelGridInfo

		#echo $modFile
		# parse MODEL variable name and season from coupled model input file
		set var=`echo $modFile | cut -d . -f5`
		set season=`echo $modFile | cut -d . -f6`
		
		# define corresponding OBSERVATIONS filename
		setenv obsFile $OBSDIR$OBSNAME.$pName.$OBSDATES.$var.$season.nc
		#echo $obsFile
		#cdo --silent griddes $obsFile
		
		# regrid OBSERVATIONS to MODEL grid
		setenv tempFile $OBSDIR"temp.nc"
		cdo --silent remapbil,./FileManipulationScripts/ModelGridInfo $obsFile $tempFile
		
		# define output file, to be written to newly created directory under $MODDIR
		setenv dFile $DIRO$MODNAME"_minus_"$OBSNAME.$pName.$var.$season.nc
		#echo $dFile
		
		# create difference file using cdo operators
		cdo --silent sub $modFile $tempFile $dFile	# -s is for "silent mode" (less output to screen)
		setenv attr '"'$MODNAME' - '$OBSNAME'"'
		echo "ncatted -O -a difference_definition,global,c,c,"$attr" "$dFile >> ./FileManipulationScripts/attr.sh
		
		# remove model grid info file
		cp ./FileManipulationScripts/ModelGridInfo ./FileManipulationScripts/ModelGridInfo_save
		rm ./FileManipulationScripts/ModelGridInfo
		
	end
		
end

foreach pName ("make_L1.7_RH_byWindSST_PDF")
	echo "Processing output generated by "$pName
	foreach modFile ($MODDIR$MODNAME.$pName.*.nc)
		#echo $modFile
		# parse variable name and season from coupled model input file
		set season=`echo $modFile | cut -d . -f5`
		
		# define corresponding uncoupled filename
		setenv obsFile $OBSDIR$OBSNAME.$pName.$OBSDATES.$season.nc
		#echo $obsFile
		
		# define output file, to be written to newly created directory under $MODDIR
		setenv dFile $DIRO$MODNAME"_minus_"$OBSNAME.$pName.$season.nc
		
		# create difference file using cdo operators
		cdo --silent sub $modFile $obsFile $dFile	# -s is for "silent mode" (less output to screen)
		setenv attr $MODNAME" minus "$OBSNAME
		#echo $attr
		setenv attr '"'$MODNAME' - '$OBSNAME'"'
		echo "ncatted -O -a difference_definition,global,c,c,"$attr" "$dFile >> ./FileManipulationScripts/attr.sh
		
	end
end

#============= Level 2 difference files ==================
#
#	NOTE: 	THESE FILES ARE PROCESSED WITH AN NCL SCRIPT
#			BECAUSE CDO DOES NOT LIKE REGRIDDING THESE 
#			ARRAYS SINCE THEY ARE "GENERIC" GRID TYPES.
#
foreach pName ("make_L2.1_regression_propagation" )
	echo "Processing output generated by " $pName
	foreach modFile ($MODDIR$MODNAME.$pName.$MODDATES.*.nc)

		setenv	progGen	$pName
		echo ""
		echo ""
		echo ""
		echo $modFile

		# parse variable name and season from coupled model input file
		set var1=`echo $modFile | cut -d . -f5`
		set var2=`echo $modFile | cut -d . -f6`
		set season=`echo $modFile | cut -d . -f7`
		
		# define variables to pass to NCL code
		setenv	inVar1	$var1
		setenv	inVar2	$var2
		setenv 	inFile 	$modFile
		setenv	iSeason	$season
		
		# define corresponding uncoupled filename
		setenv obsFile $OBSDIR$OBSNAME.$pName.$OBSDATES.$var1.$var2.$season.nc

		echo $obsFile
		echo $var1
		echo $var2
		
		ncl -Q ./FileManipulationScripts/make_L2.1_differences_DiffGrid.ncl
		
 		setenv dFile $DIRO$MODNAME"_minus_"$OBSNAME.$pName.$var1.$var2.$season.nc
 		setenv attr '"'$MODNAME' - '$OBSNAME'"'
		echo "ncatted -O -a difference_definition,global,c,c,"$attr" "$dFile >> ./FileManipulationScripts/attr.sh
	end
end

foreach pName ("make_L2.2_regression_nopropagation" )
	echo "Processing output generated by " $pName
	foreach modFile ($MODDIR$MODNAME.$pName.$MODDATES.*.nc)

		setenv	progGen	$pName
		echo ""
		echo ""
		echo ""
		echo $modFile

		# parse variable name and season from coupled model input file
		set var1=`echo $modFile | cut -d . -f5`
		set var2=`echo $modFile | cut -d . -f6`
		set season=`echo $modFile | cut -d . -f7`
		
		# define variables to pass to NCL code
		setenv	inVar1	$var1
		setenv	inVar2	$var2
		setenv 	inFile 	$modFile
		setenv	iSeason	$season
		
		# define corresponding uncoupled filename
		setenv obsFile $OBSDIR$OBSNAME.$pName.$OBSDATES.$var1.$var2.$season.nc

		echo $obsFile
		echo $var1
		echo $var2
		
		ncl -Q ./FileManipulationScripts/make_L2.2_differences_DiffGrid.ncl
		
 		setenv dFile $DIRO$MODNAME"_minus_"$OBSNAME.$pName.$var1.$var2.$season.nc
 		setenv attr '"'$MODNAME' - '$OBSNAME'"'
		echo "ncatted -O -a difference_definition,global,c,c,"$attr" "$dFile >> ./FileManipulationScripts/attr.sh
	end
end

#============= Level 3 difference files ==================
foreach pName ("make_L3.1_regression_map" )
	echo "Processing output generated by " $pName
	foreach modFile ($MODDIR$MODNAME.$pName.$MODDATES.*.nc)

		# generate OBS grid info (for each file, even though redundant)
		cdo --silent griddes $modFile >> ./FileManipulationScripts/ModelGridInfo

		#echo $modFile
		# parse MODEL variable name and season from coupled model input file
		set var1=`echo $modFile | cut -d . -f5`
		set var2=`echo $modFile | cut -d . -f6`
		set season=`echo $modFile | cut -d . -f7`
		
		# define corresponding OBSERVATIONS filename
		setenv obsFile $OBSDIR$OBSNAME.$pName.$OBSDATES.$var1.$var2.$season.nc
		
		# regrid OBSERVATIONS to MODEL grid
		setenv tempFile $OBSDIR"temp.nc"
		cdo --silent remapbil,./FileManipulationScripts/ModelGridInfo $obsFile $tempFile

		# define output file, to be written to newly created directory under $MODDIR
		setenv dFile $DIRO$MODNAME"_minus_"$OBSNAME.$pName.$var1.$var2.$season.nc
		#echo $dFile
		
		# create difference file using cdo operators
		cdo --silent sub $modFile $tempFile $dFile	# -s is for "silent mode" (less output to screen)
		setenv attr '"'$MODNAME' - '$OBSNAME'"'
		echo "ncatted -O -a difference_definition,global,c,c,"$attr" "$dFile >> ./FileManipulationScripts/attr.sh
	end
end


# foreach pName ("make_L3.2_MSE_PDF_lineplots" )
# end

foreach pName ( "make_L3.3_MSE_ModProjectionMaps" )
	echo "Processing output generated by " $pName
	foreach modFile ($MODDIR$MODNAME.$pName.$MODDATES.*.nc)

		# generate OBS grid info (for each file, even though redundant)
		cdo --silent griddes $modFile >> ./FileManipulationScripts/ModelGridInfo

		#echo $modFile
		# parse MODEL variable name and season from coupled model input file
		set season=`echo $modFile | cut -d . -f5`

		# define corresponding OBSERVATIONS filename
		setenv obsFile $OBSDIR$OBSNAME.$pName.$OBSDATES.$season.nc
		
		# regrid OBSERVATIONS to MODEL grid
		setenv tempFile $OBSDIR"temp.nc"
		cdo --silent remapbil,./FileManipulationScripts/ModelGridInfo $obsFile $tempFile

		# define output file, to be written to newly created directory under $MODDIR
		setenv dFile $DIRO$MODNAME"_minus_"$OBSNAME.$pName.$season.nc
		#echo $dFile
		
		# create difference file using cdo operators
		cdo -s sub $modFile $tempFile $dFile	# -s is for "silent mode" (less output to screen)
		setenv attr '"'$MODNAME' - '$OBSNAME'"'
		echo "ncatted -O -a difference_definition,global,c,c,"$attr" "$dFile >> ./FileManipulationScripts/attr.sh
	end
end
#============== add global attribute with short file description ==============
chmod +x ./FileManipulationScripts/attr.sh
./FileManipulationScripts/attr.sh
rm ./FileManipulationScripts/attr.sh






